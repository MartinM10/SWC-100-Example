# Sistema de Loter√≠a Smart Contract üé≤

## Tabla de Contenidos

- [Introducci√≥n a la Seguridad en Web3 üõ°Ô∏è](#introducci√≥n-a-la-seguridad-en-web3-Ô∏è)
- [Requisitos üõ†Ô∏è](#requisitos-Ô∏è)
  - [IDE](#ide)
  - [Foundry](#foundry)
  - [Primeros Pasos](#primeros-pasos)
- [Descripci√≥n del Smart Contract üìú](#descripci√≥n-del-smart-contract-)
  - [Estructura del Smart Contract üèóÔ∏è](#estructura-del-smart-contract-Ô∏è)
  - [Explicaci√≥n Detallada de las Funciones üîç](#explicaci√≥n-detallada-de-las-funciones-)
  - [Diagrama del Smart Contract üìé](#diagrama-del-smart-contract-)
- [Pruebas y Seguridad üß™](#pruebas-y-seguridad-)
  - [Casos de Prueba üìë](#casos-de-prueba-)
- [Vulnerabilidad Intencional üö®](#vulnerabilidad-intencional-)
- [Posible Soluci√≥n ‚úçÔ∏è](#posible-soluci√≥n-Ô∏è)
  - [Proof Of Concept ü•∑](#proof-of-concept-)
- [Conclusi√≥n üí≠](#conclusi√≥n-)
- [Agradecimientos üßæ](#agradecimientos-)

## Introducci√≥n a la Seguridad en Web3 üõ°Ô∏è

¬°Bienvenido al mundo de Web3! Los contratos inteligentes son la columna vertebral de las aplicaciones descentralizadas, pero _"un gran poder conlleva una gran responsabilidad"_ ü¶∏. La seguridad en Web3 es fundamental, ya que las vulnerabilidades pueden llevar a p√©rdidas financieras significativas. Este repositorio muestra un simple contrato inteligente de sistema de loter√≠a, destacando tanto su funcionalidad como una vulnerabilidad intencional. El objetivo es demostrar la importancia de las pr√°cticas de codificaci√≥n segura y proporcionar una experiencia pr√°ctica en la identificaci√≥n y mitigaci√≥n de riesgos de seguridad.

## Requisitos üõ†Ô∏è

### IDE

Vamos a necesitar un entorno de desarrollo integrado, podemos utilizar cualquier IDE que nos guste, por ejemplo:

- [Visual Studio Code](https://code.visualstudio.com/)

### Foundry

Lo siguiente que necesitamos es instalar un framework de desarrollo para Solidity.

> [!NOTE]
> Puedes utilizar [Remix](https://remix.ethereum.org/), un IDE online para Solidity, pero los tests los tendr√≠as que ejecutar de forma manual. En esta ocasi√≥n utilizaremos [Foundry](https://book.getfoundry.sh/) para automatizar los tests.

Foundry est√° compuesto por cuatro componentes:

> - [**Forge**](https://github.com/foundry-rs/foundry/blob/master/crates/forge): Ethereum Testing Framework
> - [**Cast**](https://github.com/foundry-rs/foundry/blob/master/crates/cast): Una herramienta de l√≠nea de comandos para realizar llamadas RPC a Ethereum. Permitiendo interactuar con contratos inteligentes, enviar transacciones o recuperar cualquier tipo de datos de la Blockchain mediante la consola.
> - [**Anvil**](https://github.com/foundry-rs/foundry/blob/master/crates/anvil): Un nodo local de Ethereum, similar a Ganache, el cual es desplegado por defecto durante la ejecuci√≥n de los tests.
> - [**Chisel**](https://github.com/foundry-rs/foundry/blob/master/crates/chisel): Un REPL de solidity, muy r√°pido y √∫til durante el desarrollo de contratos o testing.

**¬øPor qu√© Foundry?**

> - Es el framework m√°s r√°pido
> - Permite escribir test y scripts en Solidity, minimizando los cambios de contexto
> - Cuenta con much√≠simos cheatcodes para testing y debugging

La forma recomendada de instalarlo es mediante la herramienta **foundryup**. A continuaci√≥n vamos a realizar la instalaci√≥n paso a paso, pero si quieres realizar una instalaci√≥n libre de dependencias, puedes seguir las instrucciones de instalaci√≥n de [este repositorio](https://github.com/hardenerdev/smart-contract-auditor).

#### Instalaci√≥n

> [!NOTE]
> Si usas Windows, necesitar√°s instalar y usar [Git BASH](https://gitforwindows.org/) o [WSL](https://learn.microsoft.com/en-us/windows/wsl/install) como terminal, ya que Foundryup no soporta Powershell o Cmd.

En la terminal ejecuta:

```Powershell
curl -L https://foundry.paradigm.xyz | bash
```

Como resultado obtendr√°s algo parecido a esto:

```shell
consoleDetected your preferred shell is bashrc and added Foundry to Path run:source /home/user/.bashrcStart a new terminal session to use Foundry
```

Ahora simplemente escribe `foundryup` en la terminal y pulsa `Enter`. Esto instalar√° los cuatro componentes de Foundry: _forge_, _cast_, _anvil_ y _chisel_.

Para confimar la correcta instalaci√≥n escribe `forge --version`. Deber√≠as de obtener la versi√≥n instalada de forge:

```shell
Forge version x.x.x
```

Si no has obtenido la versi√≥n, es posible que necesites a√±adir Foundry a tu PATH. Para ello, puedes ejecutar lo siguiente:

```shell
cd ~echo 'source /home/user/.bashrc' >> ~/.bash_profile
```

Si a√∫n as√≠ sigues teniendo problemas con la instalaci√≥n, puedes seguir las instrucciones de instalaci√≥n de Foundry en su [repositorio](https://book.getfoundry.sh/getting-started/installation).

A√∫n as√≠, si no puedes instalar Foundry, no te preocupes, puedes seguir el taller utilizando [Remix](https://remix.ethereum.org/), un IDE online para Solidity.

### Primeros Pasos

Lo primero que vamos a hacer es clonar el repositorio del taller. Para ello, abre una terminal y ejecuta:

```shell
# Clonamos el repo:
https://github.com/MartinM10/VulnerableLottery.git

# Abrimos la carpeta creada
cd VulnerableLottery
```

A continuaci√≥n instalaremos las dependencias y compilaremos el proyecto para comprobar que todo est√° correcto.

```shell
# Instalamos las dependencias
forge install foundry-rs/forge-std --no-commit

# Compilamos el proyecto
forge build
```

Con esto ya tendr√≠amos todo lo necesario para ejecutar los tests y probar nuestro smart contract üëç.

## Descripci√≥n del Smart Contract üìú

El contrato inteligente [`Lottery.sol`](src/Lottery.sol) permite que m√∫ltiples loter√≠as se ejecuten simult√°neamente. Cada loter√≠a tiene las siguientes caracter√≠sticas:

- Los participantes pueden comprar boletos por un precio especificado.
- Solo se permite un boleto por participante por loter√≠a.
- Al final de la loter√≠a, se elige un ganador al azar y se le otorga el bote acumulado. Con lo cual, siempre hay un ganador en cada loter√≠a.

### Estructura del Smart Contract üèóÔ∏è

A continuaci√≥n se muestra una visi√≥n general del contrato [`Lottery.sol`](src/Lottery.sol):

1. **Variables y Estructuras**:

   - `owner`: Direcci√≥n del propietario del contrato.
   - `lotteryCount`: Contador del n√∫mero de loter√≠as.
   - `LOTTERY_DURATION`: Duraci√≥n de cada loter√≠a.
   - `Lottery` struct: Contiene los detalles de cada loter√≠a (ID, jugadores, precio del boleto, tiempo de finalizaci√≥n, bote acumulado, estado activo, y `hasTicket`).
   - `lotteries` mapping: Diccionario que contiene todas las loter√≠as. Devuelve la `Lottery` (struct) que se le indica en la clave (uint256).

2. **Funciones**:
   - `startLottery(uint256 _ticketPrice)`: Inicia una nueva loter√≠a.
   - `buyTicket(uint256 _lotteryId)`: Permite a un participante comprar un boleto para una loter√≠a espec√≠fica.
   - `endLottery(uint256 _lotteryId)`: Finaliza una loter√≠a y selecciona un ganador.
   - `_random(uint256 _numPlayers)`: Genera un n√∫mero aleatorio basado en la marca de tiempo del bloque y la dificultad.
   - `_sendPrize(address payable _winner, uint256 _prize)`: Env√≠a el premio al ganador (contiene una vulnerabilidad).
   - `cancelLottery(uint256 _lotteryId)`: Cancela una loter√≠a si no hay jugadores.
   - `getContractBalance()`: Devuelve el balance total del contrato.
   - `getLotteryInfo(uint256 _lotteryId)`: Devuelve informaci√≥n sobre una loter√≠a espec√≠fica.
   - `getLotteryPlayers(uint256 _lotteryId)`: Devuelve la lista de jugadores en una loter√≠a espec√≠fica.

### Explicaci√≥n Detallada de las Funciones üîç

A continuaci√≥n se detallan algunas de las funciones m√°s destacables del contrato

#### startLottery

```solidity
function startLottery(uint256 _ticketPrice) external onlyOwner {
    lotteryCount++;
    Lottery storage newLottery = lotteries[lotteryCount];
    newLottery.lotteryId = lotteryCount;
    newLottery.ticketPrice = _ticketPrice;
    newLottery.lotteryEndTime = block.timestamp + LOTTERY_DURATION;
    newLottery.prizePool = 0;
    newLottery.isActive = true;

    emit LotteryStarted(lotteryCount, newLottery.lotteryEndTime);
}
```

- Prop√≥sito: Inicializa y comienza una nueva loter√≠a con un precio de boleto especificado. ‚úÖ
- Modificadores: onlyOwner asegura que solo el propietario del contrato puede iniciar una loter√≠a. ‚úÖ

#### buyTicket

```solidity
function buyTicket(uint256 _lotteryId) external payable lotteryActive(_lotteryId) onlyOneTicketPerAccount(_lotteryId) {
    require(msg.value == lotteries[_lotteryId].ticketPrice, "Incorrect ticket price");
    Lottery storage lottery = lotteries[_lotteryId];
    lottery.players.push(payable(msg.sender));
    lottery.prizePool += msg.value;
    lottery.hasTicket[msg.sender] = true;

    emit TicketPurchased(_lotteryId, msg.sender, msg.value);
}
```

- Prop√≥sito: Permite a los usuarios comprar un boleto para una loter√≠a espec√≠fica. ‚úÖ
- Modificadores:
  - lotteryActive: Asegura que la loter√≠a est√° activa. ‚úÖ
  - onlyOneTicketPerAccount: Asegura que cada usuario solo puede comprar un boleto por loter√≠a. ‚úÖ

#### endLottery

```solidity
function endLottery(uint256 _lotteryId) external onlyOwner lotteryActive(_lotteryId) {
    Lottery storage lottery = lotteries[_lotteryId];
    require(block.timestamp >= lottery.lotteryEndTime, "Lottery duration has not elapsed");
    require(lottery.players.length > 0, "No players in the lottery");

    lottery.isActive = false;
    lottery.prizePool = 0; // Reset the prize pool

    uint256 winnerIndex = _random(lottery.players.length);
    address payable winner = lottery.players[winnerIndex];

    _sendPrize(winner, lottery.prizePool);

    emit WinnerDeclared(_lotteryId, winner, lottery.prizePool);
}
```

- Prop√≥sito: Finaliza una loter√≠a y selecciona un ganador. ‚úÖ
- Modificadores:
  - onlyOwner: Asegura que solo el propietario del contrato puede finalizar una loter√≠a. ‚úÖ
  - lotteryActive: Asegura que la loter√≠a est√° activa. ‚úÖ

### Diagrama del Smart Contract üìé

A continuaci√≥n se muestra un diagrama que muestra el funcionamiento m√°s destacable del smart contract de manera visual. Generado con [draw.io](https://app.diagrams.net/)

![Diagrama_01](/resources/LotteryDiagram.png)

## Pruebas y Seguridad üß™

### Casos de Prueba üìë

> [!NOTE]
> Para la ejecuci√≥n de los test automatizados debes haber realizado previamente los [Primeros Pasos](#primeros-pasos). Una vez hayamos instalado foundry y compilado el proyecto se pueden ejecutar los tests con el siguiente comando

```shell
forge test --match-contract Lottery
```

Tras ejecutar el comando deber√≠as ver que todos los tests se han pasado correctamente
![TestPassed_01](/resources/TestPassed.png)

> [!WARNING]
> Los tests no son infalibles, y en la mayor√≠a de los casos son escritos por el mismo desarrollador que dise√±√≥ el contrato, lo que significa que pueden estar sesgados.

> [!CAUTION]
> Aunque el c√≥digo pase los tests correctamente y √©stos no den ning√∫n tipo de error, ¬øSignifica que el c√≥digo es seguro? **NO**

En el contrato [`Lottery.t.sol`](test/Lottery.t.sol) tienes algunos casos de prueba importantes para asegurarte de que el contrato funciona correctamente:

1. **testMultiUserTicketPurchase**: Inicia una loter√≠a, realiza 1 compra de tickets desde 3 cuentas diferentes (1 ticket por cuenta), y comprueba que el balance del contrato es correcto.
2. **testMultiUserLotteryEnd**: Simula una loter√≠a desde el inicio hasta el final, realizando la compra de 1 ticket desde 3 cuentas diferentes.
3. **testMultiUserOneTicketPerAccount**: Verifica que una loter√≠a solo permita 1 ticket por cuenta.

```solidity
function testMultiUserTicketPurchase() public {
    lottery.startLottery(1 ether);

    // User1 buys a ticket
    vm.startPrank(user1);
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();

    // User2 buys a ticket
    vm.startPrank(user2);
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();

    // User3 buys a ticket
    vm.startPrank(user3);
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();

    (, , uint256 prizePool, ) = lottery.getLotteryInfo(1);
    assertEq(
        prizePool,
        3 ether,
        "Prize pool should be 3 ether after three tickets purchased"
    );
    console.log(
        "Multi-user ticket purchase simulated successfully, prize pool:",
        prizePool
    );

    // Check contract balance
    uint256 contractBalance = lottery.getContractBalance();
    assertEq(
        contractBalance,
        3 ether,
        "Contract balance should be 3 ether after ticket purchases"
    );
    console.log("Contract balance is correct:", contractBalance);
  }

  function testMultiUserLotteryEnd() public {
    lottery.startLottery(1 ether);

    // Users buy tickets
    vm.startPrank(user1);
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();

    vm.startPrank(user2);
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();

    vm.startPrank(user3);
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();

    // Skip forward in time to simulate end of lottery
    vm.warp(block.timestamp + 15 minutes);

    // End the lottery
    lottery.endLottery(1);

    (, , uint256 prizePool, bool isActive) = lottery.getLotteryInfo(1);

    assertFalse(isActive, "Lottery should be inactive after ending");
    assertEq(prizePool, 0, "Prize pool should be reset to 0 after ending");
    console.log("Multi-user lottery end simulated successfully");
  }

  function testMultiUserOneTicketPerAccount() public {
    lottery.startLottery(1 ether);

    // User1 buys a ticket
    vm.startPrank(user1);
    lottery.buyTicket{value: 1 ether}(1);

    vm.expectRevert("Only one ticket per account allowed");
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();
    console.log(
        "Verified only one ticket per account restriction for user1"
    );

    // User2 buys a ticket
    vm.startPrank(user2);
    lottery.buyTicket{value: 1 ether}(1);

    vm.expectRevert("Only one ticket per account allowed");
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();
    console.log(
        "Verified only one ticket per account restriction for user2"
    );

    // User3 buys a ticket
    vm.startPrank(user3);
    lottery.buyTicket{value: 1 ether}(1);

    vm.expectRevert("Only one ticket per account allowed");
    lottery.buyTicket{value: 1 ether}(1);
    vm.stopPrank();
    console.log(
        "Verified only one ticket per account restriction for user3"
    );
}
```

## Vulnerabilidad Intencional üö®

> [!WARNING]
> SPOILER: La vulnerabilidad se encuentra en la funci√≥n `_sendPrize`, Antes de seguir leyendo te invito a que la encuentres y razones. ¬øQu√© consecuencias puede tener esta posible vulnerabilidad?

> [!NOTE]
> La funci√≥n `_sendPrize` tiene una vulnerabilidad de visibilidad. Aunque los tests pasan correctamente, esta funci√≥n se declara como `public`, lo que permite a cualquiera llamar a esta funci√≥n y transferir los fondos del contrato, salt√°ndose el mecanismo de cerrar/finalizar una loter√≠a, lo cual, est√° claro que no es seguro. Se incluy√≥ esta vulnerabilidad para demostrar la importancia de revisar y asegurar cada aspecto del c√≥digo y para destacar que a diferencia de web2 un error de este tipo, en web3 puede provocar la p√©rdida de fondos inmediata.

```solidity
function _sendPrize(address payable _winner, uint256 _prize) public {
    _winner.transfer(_prize);
}
```

### Proof of Concept ü•∑

Para este repositorio se ha desarrollado un Smart Contract como prueba de concepto para explotar la vulnerabilidad mencionada anteriormente. La idea es simular a trav√©s de un test similar a los vistos en [los casos de prueba](#casos-de-prueba-), que un atacante se aprovecha de este posible fallo de seguridad y consigue drenar los fondos de contrato.

El contrato [ProofOfConcept.t.sol](/test/ProofOfConcept.t.sol) tiene todo lo necesario para llevar ejecutar el exploit.

> Para ejecutar el exploit debes usar el siguiente comando

```shell
forge test --match-contract ProofOfConcept -vvv
```

> El argumento `--match-contract` nos permite indicar que solo queremos ejecutar el contrato `ProofOfConcept` y el argumento `-vvv` nos activa el modo `verbose` en la ejecuci√≥n de `forge test`, de esta manera nos dar√° las trazas con m√°s informaci√≥n para saber por d√≥nde va la ejecuci√≥n y qu√© est√° ocurriendo con m√°s detalle.

Tras la ejecuci√≥n del comando deber√≠as ver algo como esto:

![ExploitImage](/resources/ExploitLottery.png)

> Donde se puede observar que:
>
> - Inicialmente:
>   - Se hacen 5 compras de tickets por 1 ether desde 5 cuentas diferentes. Por lo tanto el contrato tiene en su balance 5 ethers.
>   - El atacante tiene 1 ether.
> - Tras ejecutar el exploit:
>   - El atacante tiene un balance de 6 ether y el contrato se ha quedado con un balance de 0, es decir, el atacante ha logrado robar todos los fondos del contrato. üë§üí∞

## Posible soluci√≥n ‚úçÔ∏è

Si analizamos el c√≥digo detenidamente podemos observar que esta funci√≥n se llama desde otra funci√≥n endLottery. Adem√°s hay una peque√±a pista que se ha introducido en el c√≥digo que es el nombre de la funci√≥n `_sendPrize`, nombre que comienza por `_`. Esto nos deber√≠a indicar que se trata de una funci√≥n `internal`. Esta ser√≠a una posible soluci√≥n a esa posible explotaci√≥n, indicar que la funci√≥n debe ser `internal`. Esto implicar√≠a que esta funci√≥n solo puede ser llamada desde dentro del contrato por otra funci√≥n, e impidiendo que sea llamada y se ejecute desde fuera del contrato.  
En este repositorio se proporciona el c√≥digo del smart contract con la posible soluci√≥n mencionada [LotterySolution.sol](src/LotterySolution.sol)

> [!TIP]
> Adem√°s, ser√≠a recomendable crear un mecanismo de control de acceso para esta funci√≥n si es necesario, como permitir que solo el propietario del contrato la llame.

## Conclusi√≥n üí≠

El objetivo principal de este repositorio ha sido mostrar una posible vulnerabilidad en Smart Contracts, concreamente generar un ejemplo para la vulnerabilidad [SWC-100](https://swcregistry.io/docs/SWC-100/) con fines educativos. Es importante este tipo de actividades para atraer desarolladores del mundo web2 y remarcarles que deben tener precauci√≥n a la hora de desarrollar smart contracts, ya que un simple despiste puede provocar una gran p√©rdida de fondos o totalidad de los mismos.

En resumen:

- Hemos estudiado el contrato inteligente.
- Lo hemos analizado en busca de vulnerabilidades, y hemos encontrado una.
- Hemos realizado un ataque para comprobar que la vulnerabilidad es real.

Este es el proceso que sigue un auditor de contratos inteligentes. Interpreta y analiza el contrato en busca de vulnerabilidades, y si encuentra alguna, realiza una prueba de concepto para comprobar que es real.

Espero que hayas disfrutado del taller, y que hayas aprendido algo nuevo. Si tienes cualquier duda o sugerencia, no dudes en abrir un issue en el repositorio. Si quieres ver otras vulnerabilidades, te dejo otros repositorios desarrollados con el mismo fin y siguiendo la misma metodolog√≠a.

Repositorio de [@MartinM10](https://github.com/MartinM10) üßë‚Äçüíª

- [VulnerableRenewableEnergy](https://github.com/MartinM10/VulnerableRenewableEnergy)

Repositorios de [@ManuelBD145](https://github.com/ManuelBD145) üßë‚Äçüíª

- [VulnerableDailyLimitWallet](https://github.com/ManuelBD145/Fondos)
- [VulnerableBank](https://github.com/ManuelBD145/Bank)

> [!WARNING]
> Existe otra vulnerabilidad en el contrato que no te he mencionado, a ver si eres capaz de localizarla üïµÔ∏è. Te dejo dos enlaces como pistas
> [pista1](https://swcregistry.io/docs/SWC-120/) üëÅÔ∏è
> [pista2](https://chain.link/vrf) üëÅÔ∏è

## Agradecimientos üßæ

Quiero agradecer a por los conocimientos que me proporcionaron y por la plantilla de gu√≠a que me brindaron en el repositorio [HackerWeekX-Web3-workshop](https://github.com/Marcolopeez/HackerWeekX-Web3-workshop.git)

- ü¶∏ [@Marcolopeez](https://github.com/Marcolopeez)
- ü¶∏ [@jcsec-security](https://github.com/jcsec-security)
